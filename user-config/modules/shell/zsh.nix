{ pkgs
, lib
, config
, ...}:

{
  options = {
    shell.zsh.enable = lib.mkEnableOption "Enable Zsh.";
  };

  config = lib.mkIf config.shell.zsh.enable {
    programs.zsh = {
      enable = true;
      shellAliases = (import ./aliases-ls.nix { withEza = true; }) //
        (if pkgs.stdenv.isDarwin
         then {
           # Any aliases specific for bash can be defined here.

           # Temporary alias for setting up Homebrew PATH for the current session.
           brewsup = "export PATH=\"/opt/homebrew/bin:/opt/homebrew/sbin:${PATH+:$PATH}\"";
         }
         else {
           # Any aliases specific for bash can be defined here.
         });

      autosuggestion.enable = true;
      syntaxHighlighting.enable  = true;
      history.size = 100000;
      plugins = [
        # NOTE: This approach of having custom plugins work perfectly fine, but this
        # would also pollute the PATH with the plugins' directories. There may be a
        # better way to do this.
        {
          # This is needed to be loaded before other plugins.
          name = "early-options";
          src = ./zsh;
          file = "early-options.zsh";
        }
        {
          name = "powerlevel10k";
          src = pkgs.fetchFromGitHub {
            owner = "romkatv";
            repo = "powerlevel10k";
            rev = "v1.20.0";
            hash = "sha256-ES5vJXHjAKw/VHjWs8Au/3R+/aotSbY7PWnWAMzCR8E=";
            # NOTE: Hash was generated by selecting the revision without
            # specifyng a hash, which would emit some logs about what hash is.
          };
          file = "powerlevel10k.zsh-theme";
        }
        {
          name = "zsh-history-substring-search";
          src = pkgs.fetchFromGitHub {
            owner = "zsh-users";
            repo = "zsh-history-substring-search";
            rev = "v1.0.2";
            hash = "sha256-Ptxik1r6anlP7QTqsN1S2Tli5lyRibkgGlVlwWZRG3k=";
            # NOTE: Hash was generated by selecting the revision without
            # specifyng a hash, which would emit some logs about what hash is.
          };
        }
        {
          name = "zsh-abbr";
          src = pkgs.fetchFromGitHub {
            owner = "olets";
            repo = "zsh-abbr";
            rev = "v5.1.0";
            hash = "sha256-iKL2vn7TmQr78y0Bn02DgNf9DS5jZyh6uK9MzYTFZaA=";
            # NOTE: Hash was generated by selecting the revision without
            # specifyng a hash, which would emit some logs about what hash is.
          };
        }
        {
          name = "completion-style";
          src = ./zsh;
          file = "completion-style.zsh";
        }
        {
          name = "history-substring";
          src = ./zsh;
          file = "history-substring.zsh";
        }
        {
          name = "key-bindings";
          src = ./zsh;
          file = "key-bindings.zsh";
        }
        {
          name = "options";
          src = ./zsh;
          file = "options.zsh";
        }
        {
          name = "powerlevel10k-config";
          src = ./zsh;
          file = "p10k.zsh";
        }
      ];
    };
    xdg.configFile = {
      # ZSH abbreviation with https://github.com/olets/zsh-abbr needs a
      # dedicated configuration file.
      "zsh-abbr/user-abbreviations".source = ./zsh/zsh-abbr.txt;
    };
  };
}
